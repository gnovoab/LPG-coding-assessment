plugins {
	id 'org.springframework.boot' version '2.3.3.RELEASE'
	id 'io.spring.dependency-management' version '1.0.10.RELEASE'
	id 'java'
	id 'checkstyle'
	id 'pmd'
	id "com.github.spotbugs" version "4.3.0"
	id 'jacoco'
}

repositories {
	mavenCentral()
}

//Settings
bootJar {
	archiveFileName = 'gnovoab-lpg-service.jar'
}

group = 'com.lpg'
sourceCompatibility = JavaVersion.VERSION_14
targetCompatibility = JavaVersion.VERSION_14

checkstyle {
	configFile = file('codequality/checkstyle/checkstyle.xml')
}
tasks.withType(Checkstyle) {
	reports {
		xml.enabled false
		html.enabled true
	}
}

// configure HTML report
spotbugsMain {
	reports {
		html {
			enabled = true
			stylesheet = 'fancy-hist.xsl'
		}
	}
}

spotbugsTest {
	reports {
		html {
			enabled = true
			stylesheet = 'fancy-hist.xsl'
		}
	}
}


jacocoTestReport {
	reports {
		xml.enabled false
		csv.enabled false
		html.enabled = true
		html.destination file("${buildDir}/reports/jacoco")
	}
}


jacocoTestReport {
	dependsOn test
}

test {
	useJUnitPlatform()
	finalizedBy jacocoTestReport
}

dependencies {
	//Actuator
	implementation 'org.springframework.boot:spring-boot-starter-actuator'

	//Web
	implementation 'org.springframework.boot:spring-boot-starter-web'

	//Valdiation
	implementation 'org.springframework.boot:spring-boot-starter-validation'

	//Swagger
	implementation "org.springdoc:springdoc-openapi-ui:1.4.4"

	//JDBC
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
	implementation 'org.flywaydb:flyway-core'

	//DB
	runtimeOnly 'org.postgresql:postgresql'

	//Spotbugs
	implementation "com.github.spotbugs:spotbugs-annotations:4.1.2"

	//Test
	testImplementation('org.springframework.boot:spring-boot-starter-test') {
		exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
	}
}


